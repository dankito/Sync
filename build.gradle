// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'

        // for Android compile time annotation processing
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'

        // for Java compile time annotation processing
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.8"

        // for Spring Boot dependency injection
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.4.3.RELEASE"
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

apply from: "versions.gradle"


def checkstyleConfigFile = file("$project.rootDir/config/quality/checkstyle/checkstyle.xml")

subprojects {
    apply plugin: 'checkstyle'
    apply plugin: 'findbugs'
    apply plugin: 'pmd'

    task checkstyle(type: Checkstyle) {
        group "reporting"

        ignoreFailures = true
        configFile = checkstyleConfigFile
        source 'src'
        include '**/*.java'
        exclude '**/gen/**'

        classpath = files()

        reports {
            xml.enabled true
            html.enabled false
        }
    }

    task findBugs(type: FindBugs) {
        group "reporting"

        ignoreFailures = true
        effort = "max"
        reportLevel = "low"

        reports {
            xml.enabled true
            html.enabled false
            xml.withMessages true
        }
    }

    task pmd(type: Pmd) {
        group "reporting"

        ignoreFailures = true
        ruleSets = [ "java-basic", "java-braces", "java-strings" ]
//        ruleSets = ["java-basic", "java-braces", "java-clone", "java-coupling",
//                    "java-design", "java-naming", "java-strings"]

        reports {
            xml.enabled = true
            html.enabled = true
        }
    }
}


task clean(type: Delete) {
    delete rootProject.buildDir
}
